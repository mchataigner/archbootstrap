# Maintainer: Mathieu Chataigner <mathieu.chataigner@gmail.com>

pkgname=ipify-api-git
pkgver=1.0.r0.gff0bbfa
pkgrel=2
pkgdesc="http service written in go to echo requester IP"
arch=(
  'x86_64'
  'aarch64'
)
url="https://www.ipify.org/"
source=('git+https://github.com/mchataigner/ipify-api'
		'ipify.service')
md5sums=('SKIP'
         '1c26dd342969909f0ed9c5502c1dcc07')

license=('GPL')
makedepends=('git' 'go' 'godep')
provides=('ipify-api')
_go_module=github.com/rdegges/ipify-api
_gitname=ipify-api

prepare(){
  export GOPATH="$srcdir"/GO
  mkdir -p "$srcdir"/GO/src/$_go_module
  rm -rf "$srcdir"/GO/src/$_go_module
  ln -rTsf $_gitname "$srcdir"/GO/src/$_go_module

  # the dependencies can be fetched here if needed
  cd "$srcdir"/GO/src/$_go_module
  git clean -fxd
  godep get
}

pkgver () {
  cd $_gitname
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOFLAGS="-gcflags=all=-trimpath=${PWD} -asmflags=all=-trimpath=${PWD} -ldflags=-extldflags=-zrelro -ldflags=-extldflags=-znow"
  export GOOS="linux"

  case "$CARCH" in
	"x86_64")
	  export GOARCH="amd64"
	  ;;
	"aarch64")
	  export GOARCH="arm64"
	  ;;
  esac

  export GOPATH="$srcdir"/GO

  cd "$srcdir"/GO/src/$_go_module
  go build
}

package() {
  cd "$srcdir"/GO/src/$_go_module
  mkdir -p $pkgdir/usr/bin
  install -Dm755 ipify-api $pkgdir/usr/bin/ipify-api
  mkdir -p $pkgdir/usr/lib/systemd/system
  install -Dm644 $srcdir/ipify.service $pkgdir/usr/lib/systemd/system/ipify.service
}
